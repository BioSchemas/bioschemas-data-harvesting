#+ summary: Query to retrieve protein data
#+ description: Retrieve all properties that are mentioned in the Protein Profile. Proteins are grouped by their IRI, with multiple values possible. Defensive query assumes that data does not conform to Protein profile
#+ endpoint: https://swel.macs.hw.ac.uk/data/repositories/bioschemas
#+ defaults:
#+   - protein: https://disprot.org/DP00005
#+ tags:
#+   - Protein

PREFIX schema: <https://schema.org/>
PREFIX pav: <http://purl.org/pav/>
PREFIX void: <http://rdfs.org/ns/void#>

SELECT
    (GROUP_CONCAT(DISTINCT ?identifier;SEPARATOR=',') AS ?identifiers)
    (GROUP_CONCAT(DISTINCT ?name;SEPARATOR=',') AS ?names)
    (GROUP_CONCAT(DISTINCT ?associatedDisease;SEPARATOR=',') AS ?diseases)
    (GROUP_CONCAT(DISTINCT ?description;SEPARATOR=',') AS ?descriptions)
    (GROUP_CONCAT(DISTINCT ?annotation;SEPARATOR=',') AS ?annotations)
    (GROUP_CONCAT(DISTINCT ?encodedBy;SEPARATOR=',') AS ?genes)
    (GROUP_CONCAT(DISTINCT ?taxonomicRange;SEPARATOR=',') AS ?taxons)
    (GROUP_CONCAT(DISTINCT ?url;SEPARATOR=',') AS ?urls)
    (GROUP_CONCAT(DISTINCT ?alternateName;SEPARATOR=',') AS ?altNames)
    (GROUP_CONCAT(DISTINCT ?bioChemInteraction;SEPARATOR=',') AS ?interactions)
    (GROUP_CONCAT(DISTINCT ?bioChemSimilarity;SEPARATOR=',') AS ?similities)
    (GROUP_CONCAT(DISTINCT ?bioChemEntity;SEPARATOR=',') AS ?entities)
    (GROUP_CONCAT(DISTINCT ?sequence;SEPARATOR=',') AS ?sequences)
    (GROUP_CONCAT(DISTINCT ?molFunction;SEPARATOR=',') AS ?functions)
    (GROUP_CONCAT(DISTINCT ?representation;SEPARATOR=',') AS ?representations)
    (GROUP_CONCAT(DISTINCT ?image;SEPARATOR=',') AS ?images)
    (GROUP_CONCAT(DISTINCT ?process;SEPARATOR=',') AS ?processes)
    (GROUP_CONCAT(DISTINCT ?cellularLocation;SEPARATOR=',') AS ?locations)
    (GROUP_CONCAT(DISTINCT ?parentEntity;SEPARATOR=',') AS ?parents)
    (GROUP_CONCAT(DISTINCT ?sameAs;SEPARATOR=',') AS ?equivalences)
    (GROUP_CONCAT(DISTINCT ?dataset;SEPARATOR=',') AS ?datasets)
WHERE {
    VALUES ?class {<https://schema.org/Protein>
                    <https://bioschemas.org/Protein>
                    <http://schema.org/Protein>
                    <http://bioschemas.org/Protein>}
    GRAPH ?g {
# Bioschemas Minimal Properties
        ?_protein_iri a ?class .
        OPTIONAL {?_protein_iri schema:identifier ?identifier }
        OPTIONAL {?_protein_iri schema:name ?name }
## Bioschemas Recommended properties
        OPTIONAL {?_protein_iri schema:associatedDisease ?associatedDisease}
        OPTIONAL {?_protein_iri schema:description ?description}
        OPTIONAL {?_protein_iri schema:hasSequenceAnnotation ?annotation }
        OPTIONAL {?_protein_iri schema:includedInDataset ?dataset }
        OPTIONAL {?_protein_iri schema:isEncodedByBioChemEntity ?encodedBy}
        OPTIONAL {?_protein_iri schema:taxonomicRange ?taxonomicRange }
        OPTIONAL {?_protein_iri schema:url ?url}
## Bioschemas Optional properties
        OPTIONAL {?_protein_iri schema:alternateName ?alternateName}
        OPTIONAL {?_protein_iri schema:bioChemInteraction ?bioChemInteraction}
        OPTIONAL {?_protein_iri schema:bioChemSimilarity ?bioChemSimilarity}
        OPTIONAL {?_protein_iri schema:hasBioChemEntityPart ?bioChemEntity}
        OPTIONAL {?_protein_iri schema:hasBioPolymerSequence ?sequence}
        OPTIONAL {?_protein_iri schema:hasMolecularFunction ?molFunction}
        OPTIONAL {?_protein_iri schema:hasRepresentation ?representation }
        OPTIONAL {?_protein_iri schema:image ?image}
        OPTIONAL {?_protein_iri schema:isInvolvedInBiologicalProcess ?process}
        OPTIONAL {?_protein_iri schema:isLocatedInSubcellularLocation ?cellularLocation}
        OPTIONAL {?_protein_iri schema:isPartOfBioChemEntity ?parentEntity}
        OPTIONAL {?_protein_iri schema:sameAs ?sameAs }
    }
}
